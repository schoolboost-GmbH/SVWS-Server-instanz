plugins {
	id 'java'
}

repositories {
	mavenCentral()
}

configurations {
	codeGen.extendsFrom(jaxbRuntime, xjc)
	implementation.extendsFrom(jaxbRuntime)
}

def genDir = layout.projectDirectory.dir('src/gen')
def jaxbOutputDir = layout.projectDirectory.dir('src/gen/java')
def schemaFileXSchule = layout.projectDirectory.file('src/main/resources/xschule/xschule.xsd')
def prefixBindingFile = layout.projectDirectory.file('src/main/resources/xschule/bindings/prefixes.xjb')

tasks.register('generateSourcesXSchule', JavaExec) {
	group = 'build'
	description = 'Generiert Java-Klassen für den XSchule-Kontext aus XSD-Datei mithilfe von JAXB'
	inputs.files(schemaFileXSchule, prefixBindingFile)
	outputs.dir(jaxbOutputDir)
	classpath = configurations.codeGen
	mainClass = 'com.sun.tools.xjc.XJCFacade'
	systemProperties = ['javax.xml.accessExternalDTD': 'all'] // Zugriff auf externe DTDs erlauben

	doFirst {
		args = [
				'-d', jaxbOutputDir,
				'-b', prefixBindingFile.getAsFile().absolutePath,
				schemaFileXSchule.getAsFile().absolutePath
		]
		println "Generiere Java-Klassen für XSchule-Kontext aus Schema ${schemaFileXSchule.getAsFile().name}"
		println "Ausgabe-Verzeichnis: ${jaxbOutputDir}"
	}
}

tasks.register('cleanSourcesXSchule', Delete) {
	group = 'build'
	description = 'Entfernt die für den XSchule-Kontext generierten Java-Klassen'
	delete genDir

	doFirst {
		logger.lifecycle("Entferne Verzeichnis mit generierten XSchule-Dateien aus ${genDir.getAsFile()}")
	}
}

sourceSets {
	main {
		java {
			srcDirs += jaxbOutputDir
		}
	}
}

def filteredJavaTree = fileTree('src/gen/java') {
	exclude '**/*.java'
}

tasks.named('checkstyleMain').configure {
	source = filteredJavaTree
}

tasks.named('javadoc').configure {
	source = filteredJavaTree
}
