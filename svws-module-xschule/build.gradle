plugins {
	id 'java'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation project(':svws-asd')
	implementation project(':svws-db-dto')
}

configurations {
	codeGen.extendsFrom(jaxbRuntime, xjc)
	implementation.extendsFrom(jaxbRuntime)
}

def jaxbOutputDir = layout.buildDirectory.dir('generated/sources/java')
def schemaFileXSchule = layout.projectDirectory.file('src/main/resources/xschule/xschule.xsd')
def prefixBindingFile = layout.projectDirectory.file('src/main/resources/xschule/bindings/prefixes.xjb')

tasks.register('generateSourcesXSchule', JavaExec) {
	group = 'build'
	description = 'Generiert Java-Klassen für den XSchule-Kontext aus XSD-Datei mithilfe von JAXB'
	inputs.files(schemaFileXSchule, prefixBindingFile)
	outputs.dir(jaxbOutputDir)
	classpath = configurations.codeGen
	mainClass = 'com.sun.tools.xjc.XJCFacade'
	systemProperties = ['javax.xml.accessExternalDTD': 'all'] // Zugriff auf externe DTDs erlauben

	doFirst {
		args = [
				'-d', jaxbOutputDir.get().asFile.absolutePath,
				'-b', prefixBindingFile.getAsFile().absolutePath,
				schemaFileXSchule.getAsFile().absolutePath
		]
		println "Generiere Java-Klassen für XSchule-Kontext aus Schema ${schemaFileXSchule.getAsFile().name}"
		println "Ausgabe-Verzeichnis: ${jaxbOutputDir}"
	}
}

sourceSets {
	main {
		java {
			srcDirs += jaxbOutputDir
		}
	}
}

def filteredJavaTree = fileTree(jaxbOutputDir) {
	exclude '**/*.java'
}

tasks.named('checkstyleMain').configure {
	source = filteredJavaTree
}

tasks.named('javadoc').configure {
	source = filteredJavaTree
}

compileJava.dependsOn(generateSourcesXSchule)
sourcesJar.dependsOn(generateSourcesXSchule)
