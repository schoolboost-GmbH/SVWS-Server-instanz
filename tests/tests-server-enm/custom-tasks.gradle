import groovy.json.JsonSlurper
import svws.gradle.scripts.FileUtils

def svwsArtifactVersion = project.ext.latestRelease
def buildDirPath = layout.buildDirectory.get()
def rootProjectPath = project.rootProject.projectDir

def configDir = "${rootDir}/config/tests"
def configFile = file("$configDir/config.json").exists() ? file("$configDir/config.json") : file("$configDir/config_default.json")
def config = new JsonSlurper().parse(configFile)

/**
 * Kopiert ENM-Build-Artefakte (ZIP).
 */
tasks.register('copyENMServerBuildArtefacts') {
	description = 'Kopiert die ENM-Server-Build-Artefakte'
	dependsOn ':svws-webclient:enmserver:doZip'
	mustRunAfter(':svws-webclient:enmserver:doZip')
	inputs.file("${rootProjectPath}/svws-webclient/enmserver/build/SVWS-ENMServer-${svwsArtifactVersion}.zip")

	doLast {
		copy {
			from "${rootProjectPath}/svws-webclient/enmserver/build/SVWS-ENMServer-${svwsArtifactVersion}.zip"
			into "./build"
		}
		println("${rootProjectPath}/svws-webclient/enmserver/build/SVWS-ENMServer-${svwsArtifactVersion}.zip")
	}
}

/**
 * Entzippe die Build Artefakte in den build Ordner
 */
tasks.register('unzipENMBuildArtefact') {
	description = 'Entzippe die Build Artefakte in den build Ordner'
	dependsOn "copyENMServerBuildArtefacts"
	mustRunAfter "copyENMServerBuildArtefacts"
	inputs.files copyENMServerBuildArtefacts.outputs.files
	def outputDir = file("$buildDirPath/tempdocker/enm/dist")
	outputs.dir(outputDir)
	doFirst {
		mkdir file("./build/tempdocker/enm/dist")
	}
	doLast {
		def zipFile = file("build/SVWS-ENMServer-${svwsArtifactVersion}.zip")
		copy {
			from(zipTree(zipFile))
			into(outputDir)
		}
		println("Build Artefakte für ENM Server entpackt")
	}
}

/**
 * Kopiere die SSL Zertifikate aus dem laufenden SVWS App Container
 */
tasks.register('copySSLCerts', Exec) {
	description = 'Kopiere die SSL Zertifikate aus dem laufenden SVWS App Container'
	mustRunAfter("starteDockerContainerMitTestUmgebung")
	mustRunAfter(unzipENMBuildArtefact)

	def CONTAINER_NAME = project.ext.APP_CONTAINER_NAME
	executable 'docker'

	doFirst {
		mkdir file("./build/tempdocker/enm")
		mkdir file("./build/tempdocker/enm/certs")
		args 'cp', "${CONTAINER_NAME}:/var/ssl/.", "$buildDirPath/tempdocker/enm/certs"
	}
}

/**
 * Generiere das Dockerfile welches verwendet wird um das ENM Image zu bauen
 */
tasks.register('generateENMDockerFile') {
	description = 'Generiere das Dockerfile welches verwendet wird um das ENM Image zu bauen'
	mustRunAfter("starteDockerContainerMitTestUmgebung")
	mustRunAfter(unzipENMBuildArtefact)

	doFirst {
		mkdir file("./build/tempdocker/enm")
	}

	def dockerReqistryUrl = System.getenv('CI_SERVER') ? config.dockerRegistry : "docker.io"
	def inputFile = file('../templates/docker/template.enm.dockerfile')
	def outputFile = file("$buildDirPath/tempdocker/enm/Dockerfile")

	doLast {
		def replacements = [
				'GRADLE_PLACEHOLDER_DOCKER_REG_URL' : "${dockerReqistryUrl}",
		]

		FileUtils.replaceFilePlaceholderWithValues(inputFile, outputFile, replacements)
	}
}

/**
 * Generiere das Docker Compose File welches verwendet wird um den ENM Server zu starten
 */
tasks.register('generateENMDockerComposeFile') {
	description = 'Generiere das Docker Compose File welches verwendet wird um den ENM Server zu starten'
	mustRunAfter("starteDockerContainerMitTestUmgebung")
	mustRunAfter(unzipENMBuildArtefact)

	doFirst {
		mkdir file("./build/tempdocker/enm")
	}

	def ENM_IMAGE_NAME = project.ext.ENM_IMAGE_NAME
	def ENM_CONTAINER_NAME = project.ext.ENM_CONTAINER_NAME
	def ENM_FORWARDING_PORTS = "\"80:80\", \"443:443\""
	def inputFile = file('../templates/docker/enm.compose.yml.template')
	def outputFile = file("$buildDirPath/tempdocker/enm/compose.yml")

	doLast {
		def replacements = [
				'#GRADLE_PLACEHOLDER_ENM_IMAGE_NAME' : "${ENM_IMAGE_NAME}",
				'#GRADLE_PLACEHOLDER_ENM_CONTAINER_NAME': "${ENM_CONTAINER_NAME}",
				'#GRADLE_PLACEHOLDER_ENM_FORWARDING_PORTS': "${ENM_FORWARDING_PORTS}",
		]

		FileUtils.replaceFilePlaceholderWithValues(inputFile, outputFile, replacements)
	}
}

tasks.register('copyApacheHostConfig', Copy) {
	description = 'Kopiert die Apache2 Non SSL Host Config, für das interne Routing im ENM Container'
	dependsOn 'generateENMDockerComposeFile'
	mustRunAfter("starteDockerContainerMitTestUmgebung")
	mustRunAfter(unzipENMBuildArtefact)

	doFirst {
		mkdir file("./build/tempdocker/enm/hostconfig")
	}

	from "${rootDir}/tests/templates/config/enm.apache-config.conf"
	into "${buildDirPath}/tempdocker/enm/hostconfig"
}

tasks.register('copyApacheHostConfigSSL', Copy) {
	description = 'Kopiert die Apache2 SSL Host Config, für das interne Routing im ENM Container'
	mustRunAfter("starteDockerContainerMitTestUmgebung")
	mustRunAfter(unzipENMBuildArtefact)

	doFirst {
		mkdir file("./build/tempdocker/enm/hostconfig")
	}

	from "${rootDir}/tests/templates/config/enm.apache-config-ssl.conf"
	into "${buildDirPath}/tempdocker/enm/hostconfig"
}

/**
 * Baue das Docker Image vom ENM Server
 */
tasks.register('buildDockerImageENM', Exec) {
	description = 'Baue das Docker Image vom ENM Server'
	dependsOn generateENMDockerComposeFile
	dependsOn generateENMDockerFile
	dependsOn copySSLCerts
	dependsOn unzipENMBuildArtefact
	dependsOn copyApacheHostConfigSSL
	dependsOn copyApacheHostConfig

	mustRunAfter(unzipENMBuildArtefact)
	mustRunAfter(copyApacheHostConfigSSL)
	mustRunAfter(copyApacheHostConfig)
	mustRunAfter(copySSLCerts)

	def ENM_IMAGE_NAME = project.ext.ENM_IMAGE_NAME

	doFirst {
		executable 'docker'
		args 'build', '-t', "${ENM_IMAGE_NAME}", '-f', "$buildDirPath/tempdocker/enm/Dockerfile", '.'
	}
}

/**
 * Starte den Docker-Container für die Testumgebung des ENM Server
 */
tasks.register('starteDockerContainerMitTestUmgebungENM', Exec) {
	description = 'Starte den Docker-Container für die Testumgebung des ENM Server'
	dependsOn 'buildDockerImageENM'
	dependsOn ':tests:startTestDockerNetwork'
	def PROJECT_NAME = project.name
	doFirst {
		executable 'docker'
		workingDir "$buildDirPath/tempdocker/enm"
		args 'compose', '-p', "svws-$PROJECT_NAME", '-f', "$buildDirPath/tempdocker/enm/compose.yml", 'up', '--wait'
	}
}
