plugins {
	id 'svws.gradle.node.plugin'
}

// Führt alle Tasks von subproject-plugin.gradle in diesem Verzeichnis aus
apply from: ('../subproject-plugin.gradle')

/**
 * Konfiguration für das Subpackage bzw. die Tests
 */
ext {
	SVWS_SERVERMODE = "dev"

	// Docker Konfiguration
	APP_CONTAINER_NAME = "svws-server-e2e-tests-dev${rootProject.ext.CONTAINER_NAME_SUFFIX}"
	DB_CONTAINER_NAME = "db-e2e-tests-dev${rootProject.ext.CONTAINER_NAME_SUFFIX}"

	APP_IMAGE_NAME = "svws-server-e2e-tests-dev${rootProject.ext.CONTAINER_NAME_SUFFIX}"
	DB_SERVICE_NAME = "db-e2e-tests-dev${rootProject.ext.CONTAINER_NAME_SUFFIX}"

	// Auch für Docker. Forwarded Ports, werden nur bei lokal genutzten Container verwendet
	FORWARDED_DB_PORT = 3322
	FORWARDED_APP_PORT = 5022

	// Liste der Datenbanken die für die Tests genutzt / geseedet werden sollen
	TARGET_TEST_DATABASES = ['GymAbi01']
}

tasks.register('npmInstallPlaywright', NpmRun) {
	description = 'Installiert die Playwright Binaries/Browser'

	onlyIf {
		System.getenv('PLAYWRIGHT_SKIP_BROWSER_DOWNLOAD') != "1"
	}

	args = ['installPlaywright']
}

tasks.register('npmInstall', NpmInstall) {
	description = 'Installiert die Npm Abhängigkeiten'
	doFirst {
		environment 'PLAYWRIGHT_SKIP_BROWSER_DOWNLOAD', System.getenv('PLAYWRIGHT_SKIP_BROWSER_DOWNLOAD')
		environment 'PLAYWRIGHT_BROWSERS_PATH', System.getenv('PLAYWRIGHT_BROWSERS_PATH')
	}

	finalizedBy 'npmInstallPlaywright'
}

/**
 * Starten der Integrationstests gegen die Docker Umgebung
 */
tasks.register('testAgainstDockerEnv', NpmRun) {
	description = 'Startet die Integrationstests gegen die Docker Umgebung'
	group = 'testing'

	dependsOn ':svws-webclient:core:assemble'
	dependsOn ':svws-webclient:client:assemble'
	dependsOn 'starteDockerContainerMitTestUmgebung'
	dependsOn 'npmInstall'

	doFirst {
		mkdir file("$projectDir/build/test-results")
	}

	// Falls Exceptions auftreten führen diese nicht zum Abbruch des Scripts
	ignoreExitValue = true

	// Die Ziel-url vom Container mit der Application
	if (System.getenv('CI_SERVER')) {
		environment VITE_targetHost: "https://$project.ext.APP_CONTAINER_NAME:8443"
	} else {
		environment VITE_targetHost: "https://localhost:$project.ext.FORWARDED_APP_PORT"
	}
	outputs.upToDateWhen { false }
	outputs.dir file("$projectDir/build/test-results")

	// npm Kommando
	args = ['test']
}


/**
 * Starten der Tests gegen die lokale Applikation. Hier wird kein Docker gestartet.
 */
tasks.register('no_auto_docker_test_E2E', NpmRun) {
	group = "verification"
	dependsOn ':tests:checkIfLocalAppIsRunning'
	dependsOn ':tests:checkIfLocalDatabaseIsRunning'
	inputs.dir file("$projectDir/tests")
	args = ['test:local']
}

/**
 * Erzeugt die Codegen Dateien. Kann genutzt werden um E2E Tests aufzuzeichnen
 */
tasks.register('recordE2ETest', NpmRun) {
	dependsOn 'playwrightInstallDeps'
	mkdir file("$projectDir/build/codegen")
	args = ['codegen']
}
